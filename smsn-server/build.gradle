plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
  id 'application'
}

application {
  mainClass = 'net.fortytwo.smsn.server.SmSnServer'
}

dependencies {
  implementation project(':brain')
  implementation project(':smsn-core')
  implementation project(':smsn-models')
  implementation project(':smsn-rdf')
  implementation project(':smsn-services')

  // Hydra dependency (needed for Atom/TreeNode field access)
  implementation 'net.fortytwo.hydra:hydra-java:0.7.0'

  implementation group: 'org.apache.tinkerpop', name: 'gremlin-core', version: tinkerpopVersion
  implementation group: 'org.apache.tinkerpop', name: 'gremlin-server', version: tinkerpopVersion
  implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.8.0.201705170830-rc1'
  implementation 'org.json:json:20180130'
  implementation 'commons-io:commons-io:2.11.0'

  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4'
  implementation group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-api', version: rdf4jVersion
  implementation 'com.illposed.osc:javaosc-core:0.3'
  implementation(group: 'org.eclipse.rdf4j', name: 'rdf4j-sail-memory', version: rdf4jVersion)
  implementation group: 'org.apache.tinkerpop', name: 'neo4j-gremlin', version: tinkerpopVersion
  implementation group: 'net.fortytwo', name: 'linked-data-sail', version: rippleVersion

  // Jetty WebSocket for standalone server (migration from Gremlin Server)
  implementation 'org.eclipse.jetty:jetty-server:11.0.18'
  implementation 'org.eclipse.jetty.websocket:websocket-jetty-server:11.0.18'
  implementation 'org.eclipse.jetty.websocket:websocket-jetty-api:11.0.18'

  testImplementation "junit:junit:$junitVersion"
  testImplementation project(path: ':brain', configuration: 'testOutput')
}

test {
  // Add JVM arguments to open Java modules for Neo4j compatibility with Java 11+
  // Neo4j's older version uses reflection to access internal Java classes
  jvmArgs = [
    '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
    '--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED',
    '--add-opens', 'java.base/java.util=ALL-UNNAMED',
    '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED'
  ]
}

shadowJar {
  archiveBaseName.set('smsn-server')
  archiveClassifier.set('standalone')
  zip64 = true
  mergeServiceFiles()
  manifest {
    attributes 'Main-Class': 'net.fortytwo.smsn.server.SmSnServer'
  }
}
